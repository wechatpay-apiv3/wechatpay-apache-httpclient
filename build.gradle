plugins {
    id 'java-library'
    id 'maven'
}

group 'com.github.wechatpay-apiv3'
version '0.1.6'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    httpclient_version = "4.5.12"
    slf4j_version = "1.7.30"
    junit_version = "4.12"
    jackson_version = "2.9.10"
}

dependencies {
    api "org.apache.httpcomponents:httpclient:$httpclient_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    testImplementation "org.slf4j:slf4j-simple:$slf4j_version"
    testImplementation "junit:junit:$junit_version"
}

jar {
    manifest {
        attributes('Implementation-Title': 'wechatpay-java',
                'Implementation-Version': project.version,
                'Export-Package': 'com.wechat.pay.*'
        )
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

wrapper {
    gradleVersion = "5.4.1"
    distributionType = Wrapper.DistributionType.ALL
}